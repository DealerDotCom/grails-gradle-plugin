import org.apache.ivy.util.url.CredentialsStore

apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.grails'
version = '1.1-SNAPSHOT'
isSnapshot = version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
    mavenRepo name: "Grails Repo", urls: "http://repo.grails.org/grails/repo"
}

dependencies {
    compile gradleApi(), "org.grails:grails-launcher:1.0.0-SNAPSHOT"
    groovy fileTree(dir: new File(gradle.gradleHomeDir, 'lib'), includes: ['**/groovy-all-*.jar'])
}

uploadArchives {
    repositories {
        project.deployer = repositories.mavenDeployer {
            repository(url: "http://repo.grails.org/grails/${isSnapshot ? 'libs-snapshots-local' : 'libs-releases-local'}") {
                final username = project.properties["grailsRepoUsername"]
                final password = project.properties["grailsRepoPassword"]

                if (username && password) {
                    CredentialsStore.INSTANCE.addCredentials("Artifactory Realm", "repo.grails.org", username, password)
                }

                authentication(userName: username, password: password)
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-3"
}
