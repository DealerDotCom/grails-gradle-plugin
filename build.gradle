import org.apache.ivy.util.url.CredentialsStore

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'org.grails'
version = '1.1.0'
isSnapshot = version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
    maven { url "http://repo.grails.org/grails/repo" }
}

dependencies {
    compile gradleApi(), "org.grails:grails-launcher:1.0.0"
    groovy localGroovy()
}

uploadArchives {
    repositories {
        project.deployer = repositories.mavenDeployer {
            repository(url: "http://repo.grails.org/grails/${isSnapshot ? 'libs-snapshots-local' : 'libs-releases-local'}") {
                def username = project.hasProperty("grailsRepoUsername") ? project.grailsRepoUsername : null
                def password = project.hasProperty("grailsRepoPassword") ? project.grailsRepoPassword : null

                if (username && password) {
                    CredentialsStore.INSTANCE.addCredentials("Artifactory Realm", "repo.grails.org", username, password)
                }

                authentication(userName: username, password: password)
            }
        }
    }
}

idea {
    project {
        jdkName "1.6"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
                node.append(new XmlParser().parse(file("ideaCodeStyle.xml")))
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-6"
}
