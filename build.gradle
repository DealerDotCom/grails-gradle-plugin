apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'org.grails'
version = '1.1.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.grails.org/grails/repo" }
}

dependencies {
    compile gradleApi()
    groovy localGroovy()
    
    compile "org.grails:grails-launcher:1.0.5"
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8"
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

ext.poms = [project.install.repositories.mavenInstaller.pom]

uploadArchives { task ->
    description = "Does a maven deploy of archives artifacts"

    project.poms << repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment ->
            signing.signPom(deployment)
        }
        
        gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(task)) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeOssUsername, password: sonatypeOssPassword)
                }
                snapshotRepository(url: "http://repo.grails.org/grails/libs-snapshots-local") {
                    authentication(userName: gradleRepoUsername, password: gradleRepoUsername)
                }
            }
        }   
    }.pom
}

poms*.whenConfigured {
    it.project {
        name 'Grails Gradle Plugin'
        packaging 'jar'
        description 'Gradle plugin for Grails'
        url 'http://grails.org/'

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        scm {
            delegate.url 'scm:git@github.com:grails/grails-gradle-plugin.git'
            connection 'scm:git@github.com:grails/grails-gradle-plugin.git'
            developerConnection 'scm:git@github.com:grails/grails-gradle-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                delegate.url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'alkemist'
                name 'Luke Daley'
            }
            developer {
                id 'graemerocher'
                name 'Graeme Rocher'
            }
            developer {
                id 'pledbrook'
                name 'Peter Ledbrook'
            }
        }
    }
}

idea {
    project {
        jdkName "1.6"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
                node.append(new XmlParser().parse(file("ideaCodeStyle.xml")))
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion "1.2"
}