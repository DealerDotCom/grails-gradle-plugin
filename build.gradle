import org.apache.ivy.plugins.resolver.URLResolver

apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.grails'
version = '1.1-SNAPSHOT'

configurations {
    deploy
}

repositories {
    mavenCentral()
    mavenRepo urls: [ 'http://download.java.net/maven/2/' ]
    add(new URLResolver()) {
        name = "grails-exec"
        addArtifactPattern "http://cloud.github.com/downloads/alkemist/grails-exec/[module]-[revision].[ext]"
    }
}

dependencies {
    compile gradleApi(), ':grails-exec:1.0-SNAPSHOT'
    groovy  'org.codehaus.groovy:groovy:1.6.8'
    deploy 'org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-6'
}

if (hasProperty('codehausUsername') && hasProperty('codehausPassword')) {
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deploy

            repository(url: 'dav:https://dav.codehaus.org/repository/grails') {
                authentication(userName: codehausUsername, password: codehausPassword)
            }

            snapshotRepository(url: 'dav:https://dav.codehaus.org/snapshots.repository/grails') {
                authentication(userName: codehausUsername, password: codehausPassword)
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-3"
}
